#!/usr/bin/perl
#  vi: set sw=2 ai sm:
# vim: set sw=2 ai sm:

use strict;
use integer;
use utf8;

use POSIX;
use Data::Dumper;

use Getopt::Long;
use vars qw( $debug );
use vars qw( $target_time_step );

$target_time_step = 5;

use vars qw( $ambient_light_input $brightness_input $max_brightness_input );
use vars qw( $lid_state_input );

$ambient_light_input  = '/sys/class/hwmon/hwmon2/device/light'; # or /sys/devices/platform/applesmc.768/light
$brightness_input     = '/sys/class/backlight/intel_backlight/brightness';
$max_brightness_input = '/sys/class/backlight/intel_backlight/max_brightness';
$lid_state_input      = '/proc/acpi/button/lid/LID0/state';

use vars qw( $battery_present_input $battery_status_input $battery_capacity_input $battery_design_capacity_input
    $battery_charge_input $battery_reserve_charge_input $battery_temperature_input $battery_voltage_input );

$battery_present_input         = '/sys/class/power_supply/BAT0/present';
$battery_status_input          = '/sys/class/power_supply/BAT0/status';
$battery_capacity_input        = '/sys/class/power_supply/BAT0/charge_full_design';
$battery_design_capacity_input = '/sys/class/power_supply/BAT0/charge_full';
$battery_charge_input          = '/sys/class/power_supply/BAT0/charge_now';
$battery_reserve_charge_input  = '/sys/class/power_supply/BAT0/alarm';
$battery_temperature_input     = '/sys/class/power_supply/BAT0/temp';
$battery_voltage_input         = '/sys/class/power_supply/BAT0/voltage_now';


use vars qw( $min_acceptable_backlight_brightness $battery_design_capacity $min_acceptable_battery_charge );


sub log_generic ($$;$) {
  my($flag, $msg, $debug_level) = @_;
  $msg =~ s/(\\)/\\\1/sg;
  $msg =~ s/\r/\\r/sg;
  $msg =~ s/\n/\\n/sg;
  printf STDERR "%d%s\t%s\n", time, $flag, $msg if $debug >= $debug_level;
}

sub log_comment ($;$) {
  my($s, $debug_level) = @_;
  return log_generic '#', $s, $debug_level;
}

sub log_input ($;$) {
  my($s, $debug_level) = @_;
  return log_generic '<', $s, $debug_level;
}

sub log_output ($;$) {
  my($s, $debug_level) = @_;
  return log_generic '>', $s, $debug_level;
}

sub read_string_from_file ($) {
  my($filename) = @_;
  my $it;
  local(*INPUT, $_, $.);
  if (open INPUT, '<', $filename) {
    log_comment "reading $filename", 2;
    $it = scalar <INPUT>;
    chomp $it;
    log_input $it, 2;
    close INPUT;
  }
  return $it;
}

sub read_number_from_file ($) {
    my($filename) = @_;
    my $it = read_string_from_file $filename;
    $it += 0 if defined $it;
    return $it;
}

sub get_ambient_light () {
  my $it;
  local(*INPUT, $_);
  my $s = read_string_from_file $ambient_light_input;
  $it = [$1, $2] if defined $s && $s =~ /^\((\d+),(\d+)\)/;
  return $it;
}

sub get_max_backlight_brightness () {
  return read_number_from_file $max_brightness_input;
}

sub get_backlight_brightness () {
  return read_number_from_file $brightness_input;
}

sub set_backlight_brightness ($) {
  my($brightness) = @_;
  local(*OUTPUT, $_);
  if (open(OUTPUT, '>', $brightness_input)) {
    log_comment "setting brightness to $brightness", 1;
    print OUTPUT "$brightness\n";
    close OUTPUT;
  } else {
    log_comment "$brightness_input: creat: $!";
  }
  return get_backlight_brightness;
}

sub turn_backlight_on () {
  my $brightness = get_backlight_brightness;
  if (defined $brightness) {
    if ($brightness < $min_acceptable_backlight_brightness) {
      my $max_brightness = get_max_backlight_brightness;
      my $target = $max_brightness/10;
      $target = 1 if !$target;
      set_backlight_brightness $target;
    }
  }
}

sub get_lid_state () {
  my $state;
  local($`, $&, $', $1);
  my $s = read_string_from_file $lid_state_input;
  $state = $1 if $s =~ /^state:\s+(\S+)/;
  return $state;
}

sub get_battery_detected () {
  return read_number_from_file $battery_present_input;
}

sub get_battery_status () {
  return read_string_from_file $battery_status_input;
}

sub get_battery_design_capacity () {
  return read_number_from_file $battery_design_capacity_input;
}

sub get_battery_capacity () {
  return read_number_from_file $battery_capacity_input;
}

sub get_battery_charge () {
  return read_number_from_file $battery_charge_input;
}

sub get_battery_reserve_charge () {
  return read_number_from_file $battery_reserve_charge_input;
}

sub get_battery_temperature () {
  return read_number_from_file $battery_temperature_input;
}

sub get_battery_input_voltage () {
  return read_number_from_file $battery_voltage_input;
}

sub scan_ssid () {
  my %ssid;
  my $cmd_name = 'iwlist';
  my $cmd_path = (map { my $det = "$_/$cmd_name"; -f $det? $det: (); } qw( /sbin /usr/sbin /bin /usr/bin ))[0];
  my @cmd = ($cmd_path, 'scan');
  local(*INPUT, $_, $1);
  my $h = open(INPUT, '-|');
  die "scan_ssid: Can't find $cmd_name\n" unless $cmd_name;
  die "scan_ssid: $cmd_name: fork: $!\n" unless defined $h;
  if ($h) {
    my $bssid;
    while (<INPUT>) {
      chomp;
      if (/^\s*Cell \S+ - Address: (\S+)/) {
	$bssid = $1;
      } elsif (/ESSID:"(.*)"\s*/) {
	$ssid{$1}->{$bssid} = 1;
      }
    }
    close INPUT;
  } else {
    POSIX::close(2); # close STDERR
    POSIX::dup(1); # dup STDOUT into STDERR
    exec { $cmd_name } @cmd;
    die "scan_ssid: $cmd_name: exec: $!\n";
  }
  return \%ssid;
}

sub probe_environment () {
  my $max_backlight_brightness = get_max_backlight_brightness;
  $min_acceptable_backlight_brightness = $max_backlight_brightness/100;
  $min_acceptable_backlight_brightness = 1 if !$min_acceptable_backlight_brightness;
  log_comment "brightness: max=$max_backlight_brightness -> min acceptable=$min_acceptable_backlight_brightness", 2;

  $battery_design_capacity = get_battery_design_capacity;
  $min_acceptable_battery_charge = get_battery_reserve_charge;
}

sub main_loop () {
  my $t0 = time;
  for (;;) {
    my $t = time;
    my $deadline = $t + $target_time_step;
    log_comment "time=$t -> deadline=$deadline", 3;
    my $light = get_ambient_light;
    my $lid_state = get_lid_state;
    log_comment sprintf("ambient light left=%d, right=%d; lid state %s", $light->[0], $light->[1], $lid_state), 1;
    turn_backlight_on if $light->[0] || $light->[1] || $lid_state eq 'open';
    my $battery_charge = get_battery_charge;
    if ($battery_charge < $min_acceptable_battery_charge) {
      log_comment "battery charge $battery_charge is too low (< $min_acceptable_battery_charge)";
      log_comment "attempting hibernation";
      system { 'swapon' } 'swapon', '-a';
      system { 'pm-hibernate' } 'pm-hibernate';
    }
    my $ssid = scan_ssid;
    if (time < $deadline) {
      my $dt = $deadline - time;
      log_comment "sleeping for $dt s", 3;
      sleep $dt;
    }
  }
}

Getopt::Long::config('bundling');
GetOptions(
  'd|debug' => sub { $debug += 1; },
) || exit(1);

probe_environment;
main_loop;


