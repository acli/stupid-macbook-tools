#!/usr/bin/perl
#  vi: set sw=2 ai sm:
# vim: set sw=2 ai sm:

use strict;
use integer;
use utf8;

use POSIX;
use Data::Dumper;

use Getopt::Long;
use vars qw( $target_time_step );

$target_time_step = 5;

use vars qw( $ambient_light_input $brightness_input $max_brightness_input );

$ambient_light_input  = '/sys/class/hwmon/hwmon2/device/light'; # or /sys/devices/platform/applesmc.768/light
$brightness_input     = '/sys/class/backlight/intel_backlight/brightness';
$max_brightness_input = '/sys/class/backlight/intel_backlight/max_brightness';

sub log_generic ($$;$) {
  my($flag, $msg, $debug_level) = @_;
  $msg =~ s/(\\)/\\\1/sg;
  $msg =~ s/\r/\\r/sg;
  $msg =~ s/\n/\\n/sg;
  printf STDERR "%d%s\t%s\n", time, $flag, $msg;
}

sub log_comment ($;$) {
  my($s, $debug_level) = @_;
  return log_generic '#', $s, $debug_level;
}

sub log_input ($;$) {
  my($s, $debug_level) = @_;
  return log_generic '<', $s, $debug_level;
}

sub log_output ($;$) {
  my($s, $debug_level) = @_;
  return log_generic '>', $s, $debug_level;
}

sub get_ambient_light () {
  my $it;
  local(*INPUT, $_);
  if (open(INPUT, '<', $ambient_light_input)) {
    my $s = scalar <INPUT>;
    if (defined $s) {
      chomp $s;
      log_input $s, 2;
      $it = [$1, $2] if $s =~ /^\((\d+),(\d+)\)/;
    }
    close INPUT;
  }
  return $it;
}

sub get_max_backlight_brightness () {
  my $max_brightness;
  local(*INPUT, $_);
  if (open(INPUT, '<', $max_brightness_input)) {
    $max_brightness = scalar <INPUT>;
    chomp $max_brightness;
    close INPUT;
  }
  return $max_brightness;
}

sub get_backlight_brightness () {
  my $brightness;
  local(*INPUT, $_);
  if (open(INPUT, '<', $brightness_input)) {
    $brightness = scalar <INPUT>;
    chomp $brightness;
    close INPUT;
  }
  return $brightness;
}

sub set_backlight_brightness ($) {
  my($brightness) = @_;
  local(*OUTPUT, $_);
  if (open(OUTPUT, '>', $brightness_input)) {
    log_comment "setting brightness to $brightness", 1;
    print OUTPUT "$brightness\n";
    close OUTPUT;
  } else {
    log_comment "$brightness_input: creat: $!";
  }
  return get_backlight_brightness;
}

sub turn_backlight_on () {
  my $max_brightness = get_max_backlight_brightness;
  my $brightness = get_backlight_brightness;
  if (defined $max_brightness && defined $brightness) {
    my $threshold = $max_brightness/100;
    $threshold = 1 if !$threshold;
    log_comment "max_brightness=$max_brightness, brightness=$brightness -> threshold=$threshold", 2;
    if ($brightness < $threshold) {
      my $target = $max_brightness/10;
      $target = 1 if !$target;
      set_backlight_brightness $target;
    }
  }
}

sub scan_ssid () {
  my %ssid;
  my $cmd_name = 'iwlist';
  my $cmd_path = (map { my $det = "$_/$cmd_name"; -f $det? $det: (); } qw( /sbin /usr/sbin /bin /usr/bin ))[0];
  my @cmd = ($cmd_path, 'scan');
  local(*INPUT, $_, $1);
  my $h = open(INPUT, '-|');
  die "scan_ssid: Can't find $cmd_name\n" unless $cmd_name;
  die "scan_ssid: $cmd_name: fork: $!\n" unless defined $h;
  if ($h) {
    my $bssid;
    while (<INPUT>) {
      chomp;
      if (/^\s*Cell \S+ - Address: (\S+)/) {
	$bssid = $1;
      } elsif (/ESSID:"(.*)"\s*/) {
	$ssid{$1}->{$bssid} = 1;
      }
    }
    close INPUT;
  } else {
    POSIX::close(2); # close STDERR
    POSIX::dup(1); # dup STDOUT into STDERR
    exec { $cmd_name } @cmd;
    die "scan_ssid: $cmd_name: exec: $!\n";
  }
  return \%ssid;
}

sub main_loop () {
  my $t0 = time;
  for (;;) {
    my $t = time;
    my $deadline = $t + $target_time_step;
    log_comment "time=$t -> deadline=$deadline", 3;
    my $light = get_ambient_light;
    turn_backlight_on if $light->[0] || $light->[1];
    my $ssid = scan_ssid;
    if (time < $deadline) {
      my $dt = $deadline - time;
      log_comment "sleeping for $dt s", 3;
      sleep $dt;
    }
  }
}

main_loop;


